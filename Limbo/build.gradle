plugins {
    id 'java'
}

archivesBaseName = "limbo"
version = project.limbo_plugin_version

repositories {
    maven { url "https://repo.loohpjames.com/repository/" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/public/" }
    maven { url "https://jitpack.io/" }
}

dependencies {
    depencancy project(':Shared')
    depencancy "org.apache.commons:commons-io:${project.apache_commons_io_version}"

    compileOnly "com.loohp:Limbo:${project.limbo_version}"
}

jar {
    archiveVersion = project.version + "-" + rootProject.version
}

tasks.register("buildFat", Jar) {
    dependsOn tasks.build

    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    duplicatesStrategy = "exclude"
    archiveVersion = project.version + "-" + rootProject.version
    classifier = "fat"

    from files(project.sourceSets.main.output)
    from files(project.configurations.depencancy.collect { it.isDirectory() ? it : zipTree(it) })
}

processResources {
    filesMatching("plugin.yml") {
        expand "plugin_version": project.version
    }
}